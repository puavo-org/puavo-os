#!/usr/bin/env tclsh

#
# see https://www.openldap.org/doc/admin24/access-control.html
# and the manual page slapd.access(5) for documentation
#

#
# GLOBAL VARIABLES
#

set ldapsuffix     [lindex $argv 0]
set kerberos_realm [lindex $argv 1]
set samba_domain   [lindex $argv 2]

if {[llength $argv] != 3 || $ldapsuffix eq "" || $kerberos_realm eq ""
      || $samba_domain eq ""} {
  puts stderr "usage: puavo-print-acl ldapsuffix kerberos_realm samba_domain"
  exit 1
}

set rules [list]

#
# SUBROUTINES
#

proc attrs {args} {
  format {attrs="%s"} [join $args ","]
}

proc devicetype_set {devtype} {
  ldapset [format {user/puavoDeviceType & [%s]} $devtype]
}

proc dn {{_scope ""} {_branch ""}} {
  global ldapsuffix
  set branch [expr { $_branch ne "" ? "${_branch}," : ""}]
  set scope  [expr { $_scope  ne "" ? ".${_scope}"  : ""}]
  format {dn%s="%s%s"} $scope $branch $ldapsuffix
}

proc ldapset {setspec} {
  format {set="%s"} $setspec
}

proc person_affiliation {affliation} {
  format {user/puavoEduPersonAffiliation & [%s]} $affliation
}

proc puavo_dn {name} {
  format {dn.exact="uid=%s,o=puavo"} $name
}

proc rule {rulelines} {
  global rules
  set uncommented_rulelines [regsub -all -line {#.*$} $rulelines ""]
  set subst_rulelines [uplevel 1 [list subst $uncommented_rulelines]]
  set rule_elems [split $subst_rulelines]
  set nonempty_rule_elems [
    lmap _ $rule_elems { expr { $_ ne "" ? $_ : [continue] } }
  ]
  lappend rules [join $nonempty_rule_elems " "]
}

proc systemgroup {groupname} {
  global ldapsuffix
  format {group/puavoSystemGroup/member="cn=%s,ou=System Groups,%s"} \
         $groupname $ldapsuffix
}

#
# some constants
#

set Desktops        "ou=Desktops"
set Devices         "ou=Devices,ou=Hosts"
set Domain_Admins   "cn=Domain Admins,ou=Groups"
set Domain_Users    "cn=Domain Users,ou=Groups"
set Files           "ou=Files,ou=Desktops"
set Groups          "ou=Groups"
set Hosts           "ou=Hosts"
set People          "ou=People"
set Printers        "ou=Printers"
set Servers         "ou=Servers,ou=Hosts"
set System_Accounts "ou=System Accounts"
set System_Groups   "ou=System Groups"

set org_owners   [ldapset [format {[%s]/owner & user} $ldapsuffix]]
set schooladmins [ldapset "this & user/puavoAdminOfSchool"]
set admins       [ldapset [person_affiliation admin]]
set staff        [ldapset [person_affiliation staff]]
set teachers     [ldapset [person_affiliation teacher]]

# this means: admins of the same school as
# $this_thing (person, device, server) is in
set this_school_admins [ldapset "this/puavoSchool & user/puavoAdminOfSchool"]

# this means: target/what and actor/who must be in the same school
# and actor/who must also be a staff member
set this_school_staff [ldapset \
  {([staff,] + this/puavoSchool)
     & (user/puavoEduPersonAffiliation + [,] + user/puavoSchool)}]

# this means: target/what and actor/who must be in the same school
# and actor/who must also be a teacher
set this_school_teachers [ldapset \
  {([teacher,] + this/puavoSchool)
     & (user/puavoEduPersonAffiliation + [,] + user/puavoSchool)}]

#
# RULES
#

# XXX I do not think "staff" should have special rules, but historically
# XXX this has been the case.  This is why "maybe just remove this?"-comment
# XXX is there on the staff rules.  Perhaps on the early stages of Puavo
# XXX "staff" was pretty much the same as "admin"?  "staff" could be made
# XXX the same as "teacher" with equivalent permissions.

# special wide rules

rule {
  # Give slave ldap servers and other servers read access to
  # everything (in the same organisation of course) so that ldap
  # synchronization works.
  [dn subtree]
      by [puavo_dn slave]       read
      by [dn children $Servers] read
      by *                      none break
}

# set some early rules for userPassword first so that later rules
# do not accidentally grant rights to it
# "=axz" means "add authenticate delete", which excludes reading
# (simple "write" implies reading)
rule {
  [dn children $Devices]
    [attrs userPassword]
      # This must be "write", not "=axz" as below with People's passwords.
      # This is because devices belong to "simpleSecurityObject"-objectClass,
      # which requires that each time these objects are updated,
      # "userPassword"-attribute must also be set (?!?), and it can not
      # be set unless admins also can read it.  (This is not perfect but
      # this access is for admins only and the password is hashed anyway).
      by $org_owners         write
      by $this_school_admins write

      by anonymous           auth
}

rule {
  [dn children $Servers]
    [attrs userPassword]
      by $org_owners write      # the same as above
      by anonymous   auth
}

rule {
  # Allow teachers to change student passwords, but only if they belong
  # to the same school.
  [dn children $People]
    filter="(puavoEduPersonAffiliation=student)"
    [attrs userPassword]
      by $this_school_teachers =axz
      by *                     none break
}

rule {
  # If an account is locked, deny authentication.
  [dn children $People]
    filter="(puavoLocked=TRUE)"
    [attrs userPassword]
      by anonymous none
      by *         none break
}

rule {
  # Allow some privileged folks to change other peoples' passwords.
  # Note that if admins were added to the same school where organisation
  # owners are, then they could change organisation owner passwords.
  [dn children $People]
    [attrs userPassword]
      by self                =axz
      by [puavo_dn pw-mgmt]  =axz
      by $org_owners         =axz
      by $this_school_admins =axz
      by anonymous           auth
}

rule {
  # Organisation owners can change system account passwords.
  [dn children $System_Accounts]
    [attrs userPassword]
      by $org_owners write      # see comment about "write" some rules above
      by anonymous   auth
}

# Samba-related rules

# Servers need write to samba-related stuff for Samba to work correctly.

rule {
  [dn exact ou=Samba,ou=Hosts]
    [attrs entry objectClass ou]
      by [dn children $Servers] read
}

rule {
  [dn exact ou=Samba,ou=Hosts]
    [attrs children]
      by [dn children $Servers] write
}

rule {
  [dn children ou=Samba,ou=Hosts]
      by [dn children $Servers] write
}

rule {
  # This is needed for all who need to create users,
  # plus Samba in case of Windows compatibility.
  [dn exact sambaDomainName=${samba_domain}]
    [attrs sambaNextRid]
      by $org_owners            write
      by $admins                write
      by $staff                 write	# XXX maybe just remove this?
      by [dn children $Servers] write
      by *                      none break
}

rule {
  [dn exact sambaDomainName=${samba_domain}]
      by $org_owners            write
      by $admins                read
      by $staff                 read	# XXX maybe just remove this?
      by [dn children $Servers] read
}

rule {
  # This is probably needed by Samba (?).
  [dn exact ou=Idmap]
      by [dn children $Servers] write
}

rule {
  # Needed by those who can create/remove admin users.
  [dn exact $Domain_Admins]
    [attrs memberUid]
      by $org_owners write
}

rule {
  # For those who can manage users.
  [dn exact $Domain_Admins]
      by $org_owners read
      by $admins     read
      by $staff      read	# XXX maybe just remove this?
}

rule {
  # Needed by those who can create/remove users.
  [dn exact $Domain_Users]
    [attrs memberUid]
      by $org_owners write
      by $admins     write
      by $staff      write	# XXX maybe just remove this?
}

rule {
  # For those who can manage users.
  [dn exact $Domain_Users]
      by $org_owners read
      by $admins     read
      by $staff      read	# XXX maybe just remove this?
}

# Kerberos Realms related rules

rule {
  [dn subtree "ou=Kerberos Realms"]
      by [puavo_dn kadmin] write
      by [puavo_dn kdc]    read
      by *                 none break
}

# Allow removing kerberos principals for those who can do user management.
# Use filter so that only normal user kerberos principals may be removed,
# not any service principals.

rule {
  [dn exact "cn=${kerberos_realm},ou=Kerberos Realms"]
    filter="(!(krbPrincipalName=*/*@${kerberos_realm}))"
    [attrs children]
      by $org_owners write
      by $admins     write
      by $staff      write	# XXX maybe just remove this?
}

rule {
  [dn children "cn=${kerberos_realm},ou=Kerberos Realms"]
    filter="(!(krbPrincipalName=*/*@${kerberos_realm}))"
    [attrs entry]
      by $org_owners write
      by $admins     write
      by $staff      write	# XXX maybe just remove this?
}

# Samba-related rules under ou=People

rule {
  # Changes to "sambaNTPassword", "sambaPwdLastSet", "sambaPwdMustChange"
  # and "sambaPwdCanChange" happen regardless of the ACLs, through
  # smbkrb5pwd (and is thus controlled by ACLs to "userPassword").
  [dn children $People]
    [attrs sambaNTPassword]
      by [dn children $Servers] write   # XXX why is this needed?

      # be explicit that nobody should have permissions to this
      by * none
}

rule {
  [dn children $People]
    [attrs sambaAcctFlags]
      by [dn children $Servers] write
      by *                      none break
}

# rules for ou=Hosts (and subtrees)

# give wide read access to these device-related puavo DNs
# (not that these can not look under ou=Samba,ou=Hosts)
rule {
  [dn subtree $Hosts]
      by [puavo_dn monitor]      read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by *                       none break
}

# rules for ou=Devices,ou=Hosts
# (should be before specific $Hosts ACLs, because $Devices is in
# ou=Hosts subtree)

rule {
  # For those who need to register new devices and modify device settings.
  [dn exact $Devices]
    [attrs children]
      by $org_owners write
      by $admins     write
      by $staff      write	# XXX maybe just remove this?
      by anonymous   none
      by *           none break
}

rule {
  [dn exact $Devices]
    [attrs children entry objectClass ou]
      by $org_owners           read
      by [systemgroup devices] read
      by [dn children $People] read
      by [dn children $Hosts]  read
      by anonymous             auth
}

rule {
  # Allow servers to write "puavoDeviceHWInfo" to all netbooting devices.
  # This is because they can act as proxies for fatclients.
  [dn children $Devices]
    # XXX This is only needed by puavoNetbootDevices.
    # XXX Unfortunately laptops on old versions try to write through
    # XXX bootservers instead of directly to cloud puavo-rest/ldapmaster,
    # XXX thus we can not yet filter by "(objectClass=puavoNetbootDevice)".
    # XXX Apply the filter again maybe on year 2022.
    # filter="(objectClass=puavoNetbootDevice)"
    [attrs puavoDeviceHWInfo]
      by [dn children $Servers] write
      by *                      none break
}

rule {
  # Allow devices to write to write their own "puavoDeviceHWInfo".
  [dn children $Devices]
    [attrs puavoDeviceAvailableImage \
           puavoDeviceCurrentImage   \
           puavoDeviceHWInfo         \
           puavoDeviceMonitorsXML    \
           puavoDevicePrimaryUser]
      by self write
      by *    none break
}

rule {
  # Allow changing device settings for organisation owners and admins
  # who are in the same school as the device.
  [dn children $Devices]
      by $org_owners           write
      by $this_school_admins   write
      by self                  read
      by [systemgroup devices] read
      by anonymous             auth
}

# rules for ou=Servers,ou=Hosts
# (should be before $Hosts, because $Servers is in ou=Hosts subtree)

# Only organisation owners can create new servers, but other should be
# able to read some information on them, for example to access printing
# permissions.

rule {
  [dn exact $Servers]
    [attrs children]
      by $org_owners write
      by $admins     read
      by $staff      read		# XXX maybe just remove this?
      by anonymous   none
      by *           none break
}

rule {
  [dn exact $Servers]
    [attrs children entry objectClass ou]
      by $org_owners           read
      by $admins               read
      by $staff                read	# XXX maybe just remove this?
      by [systemgroup servers] read
      by anonymous             auth
}

rule {
  [dn children $Servers]
    [attrs puavoDeviceCurrentImage puavoDeviceAvailableImage puavoDeviceHWInfo]
      by self write
      by *    none break
}

rule {
  [dn children $Servers]
    [attrs entry objectClass ou puavoExport puavoHostname puavoId puavoSchool]
      by $this_school_admins read
      by $staff              read	# XXX maybe just remove this?

      # XXX This is needed by /v3/sessions printer queues server information.
      # XXX It is unclear if this is needed in the real world, but old ACLs
      # XXX have this and some session tests break without this.
      # XXX If this is actually needed maybe the attribute list could
      # XXX be made shorter.
      by [devicetype_set laptop] read

      by * none break
}

rule {
  [dn children $Servers]
      by $org_owners           write
      by [systemgroup servers] read
      by anonymous             auth
}

# rules for ou=Hosts specifically

rule {
  [dn exact $Hosts]
      by $org_owners           read
      by [dn children $Hosts]  read
      by [systemgroup devices] read
      by [systemgroup servers] read
}

# rules for ou=Printers

rule {
  [dn exact $Printers]
    [attrs entry objectClass ou]
      by $org_owners                 read
      by [dn children $Servers]      read
      by [dn children $People]       read
      by [systemgroup printerqueues] read
}

rule {
  [dn exact $Printers]
    [attrs children]
      by $org_owners                 write
      by [dn children $Servers]      write	# for puavo-sync-printers
      by [dn children $People]       read
      by [systemgroup printerqueues] read
}

rule {
  [dn children $Printers]
      by $org_owners                 write
      by [dn children $Servers]      write	# for puavo-sync-printers
      by [dn children $People]       read
      by [systemgroup printerqueues] read
}

# rules for ou=Groups

rule {
  # For those who can manage users.
  [dn exact $Groups]
    [attrs children]
      by $org_owners write
      by $admins     write
      by $staff      write	# XXX maybe just remove this?
      by *           none break
}

rule {
  [dn exact $Groups]
    [attrs children entry objectClass ou]
      by $org_owners             read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [dn children $People]   read
      by [dn children $Hosts]    read
      by [systemgroup getent]    read
}

# deny $admins and $staff write access to some school attributes
rule {
  [dn subtree $Groups]
    filter="(objectClass=puavoSchool)"
    [attrs puavoBillingInfo puavoSchoolAdmin]
      by $org_owners write	# must be explicit here, org_owners are admins
      by $admins     read
      by $staff      read	# XXX maybe just remove this?
      by *           none break
}

# Do not allow read access to People and Hosts to group membership
# lists.  We do not want ordinary people to be able to list all usernames
# in an organisation (but for teachers this is okay).
rule {
  [dn subtree $Groups]
    [attrs member memberUid]
      by $org_owners write
      by $admins     write	# XXX maybe this_school_admins instead?
      by $staff      write	# XXX maybe just remove this?
      by $teachers   read

      # Users must be able to check if they belong to a particular group.
      # This allows users to read the DNs of other users that belong to the
      # same group, but not usernames or other such user information.
      by set="this/member & user" read

      by [dn children $People]  none
      by [dn children $Devices] none
      by *                      none break
}

rule {
  # allow statistics to read some group information
  [dn subtree $Groups]
    [attrs displayName entry objectClass puavoDeviceImage puavoId]
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by *                       none break
}

rule {
  # Write permissions for those who can manage users.
  [dn subtree $Groups]
      by $org_owners             write
      by $admins                 write	# XXX maybe this_school_admins instead?
      by $staff                  write	# XXX maybe just remove this?
      by [puavo_dn puavo]        read
      by [dn children $People]   read
      by [dn children $Hosts]    read
      by [systemgroup getent]    read
}

# rules for ou=People

rule {
  # Write permissions for those who can manage users.
  [dn exact $People]
    [attrs children]
      by $org_owners write
      by $admins     write
      by $staff      write	# XXX maybe just remove this?
      by *           none break
}

rule {
  [dn exact $People]
    [attrs children entry objectClass ou]
      by [puavo_dn puavo]          read
      by [puavo_dn puavo-ticket]   read  # XXX to be removed later
      by [puavo_dn pw-mgmt]        read
      by [puavo_dn statistics]     read
      by [systemgroup addressbook] read
      by [systemgroup auth]        read
      by [systemgroup getent]      read
      by [dn children $People]     read
      by [dn children $Hosts]      read
      by anonymous                 auth
}

rule {
  [dn subtree $People]
    [attrs entry objectClass uid]
      # hosts should be able to read information on the user who is
      # the puavoDevicePrimaryUser (at least to know their username)
      by [ldapset "this & user/puavoDevicePrimaryUser"] read

      by anonymous auth
      by *         none break
}

rule {
  # allow statistics to read user role
  [dn subtree $People]
    [attrs entry objectClass puavoEduPersonAffiliation puavoSchool]
      by [puavo_dn puavo-ticket]   read  # XXX to be removed later
      by [puavo_dn statistics]     read
      by *                         none break
}

# some permissions to "addressbook"
rule {
  [dn subtree $People]
    [attrs displayName                      \
           entry                            \
           givenName                        \
           jpegPhoto                        \
           mail                             \
           objectClass                      \
           preferredLanguage                \
           puavoEduPersonPersonnelNumber    \
           puavoEduPersonReverseDisplayName \
           puavoExternalId                  \
           puavoLocale                      \
           sn                               \
           telephoneNumber                  \
           uid]
      by [systemgroup addressbook] read
      by *                         none break
}

# some permissions to "auth"
rule {
  [dn subtree $People]
    [attrs eduPersonPrincipalName    \
           entry                     \
           objectClass               \
           puavoEduPersonAffiliation \
           puavoRemovalRequestTime   \
           uid]
      by [systemgroup auth] read
      by *                  none break
}

# some permissions to service "getent"
# XXX do we need still this?  If still in use, should the name be changed?
rule {
  [dn subtree $People]
    [attrs displayName                      \
           eduPersonPrincipalName           \
           entry                            \
           gidNumber                        \
           givenName                        \
           homeDirectory                    \
           loginShell                       \
           objectClass                      \
           puavoEduPersonAffiliation        \
           puavoEduPersonReverseDisplayName \
           puavoId                          \
           puavoLocked                      \
           puavoRemovalRequestTime          \
           puavoSchool                      \
           sn                               \
           uid                              \
           uidNumber]
      by [systemgroup getent] read
      by *                    none break
}


# rules needed by puavo and pw-mgmt (password management user)
# XXX Not sure why these are needed or if puavo and pw-mgmt need a
# XXX matching set of attributes, but ACL tests explicitly tests for
# XXX most of these.
rule {
  [dn subtree $People]
    [attrs eduPersonPrincipalName    \
           entry                     \
           gidNumber                 \
           givenName                 \
           homeDirectory             \
           jpegPhoto                 \
           loginShell                \
           mail                      \
           objectClass               \
           preferredLanguage         \
           puavoAcceptedTerms        \
           puavoEduPersonAffiliation \
           puavoId                   \
           puavoLocale               \
           puavoSchool               \
           sn                        \
           telephoneNumber           \
           uid                       \
           uidNumber]
      by [puavo_dn puavo]   read
      by [puavo_dn pw-mgmt] read
      by *                  none break
}

# allow users to change some of their own information
rule {
  [dn subtree $People]
    [attrs jpegPhoto          \
           mail               \
           preferredLanguage  \
           puavoAcceptedTerms \
           puavoLocale        \
           telephoneNumber]
      by self write
      by *    none break
}

# Give hosts access to owner information (so that org.json can contain
# admin users).
# XXX Note that while the need is for owners, this checks the admin status
# XXX instead.  In general it is wrong to have owners who are not admins,
# and Puavo enforces this to some extent, but not on database level.
rule {
  [dn subtree $People]
    filter="(puavoEduPersonAffiliation=admin)"
      by [dn children $Hosts] read
      by *                    none break

}

rule {
  # Allow organisation owners and admins in the same school to
  # change peoples' information.
  [dn subtree $People]
      by $org_owners           write
      by $this_school_admins   write
      by $this_school_staff    read	# /v3/my_school_users requires this
      by $this_school_teachers read	# /v3/my_school_users requires this
      by self                  read
}

# rules for ou=Desktops

# We used to have some other branches here, but only ou=Files,ou=Desktops
# ended up in permanent use, and this contains the external files.
# Organisation owners can write these, hosts can read.

rule {
  [dn exact $Files]
    [attrs children]
      by $org_owners write
      by *           none break
}

rule {
  [dn exact $Files]
    [attrs children entry objectClass ou]
      by $org_owners          read
      by [dn children $Hosts] read
}

rule {
  [dn children $Files]
      by $org_owners          write
      by [dn children $Hosts] read
}

rule {
  [dn exact $Desktops]
    [attrs children entry objectClass ou]
      by $org_owners          read
      by [dn children $Hosts] read
}

# rules for ou=System Accounts and ou=System Groups

rule {
  [dn subtree $System_Accounts]
      by $org_owners      write
      by [puavo_dn puavo] read
}

rule {
  [dn subtree $System_Groups]
    [attrs member]
      by $org_owners         write
      by $this_school_admins write
}

rule {
  [dn subtree $System_Groups]
      by $org_owners         read
      by $this_school_admins read
}

# rules for the organisation entry
# "+dsx" = "disclose search auth"

rule {
  # Do not allow organisation owners to change some organisational
  # attributes which are meant to be set once only.
  [dn]
    [attrs entry                        \
           objectClass                  \
           puavoDomain                  \
           puavoKadminPort              \
           puavoKerberosRealm           \
           puavoRemoteDesktopPrivateKey \
           puavoRemoteDesktopPublicKey  \
           sambaDomainName]
      by $org_owners read
      by *           none break
}

rule {
  # Allow organisation owners to change many organisation settings,
  # and most other entities to read them.
  [dn]
      by $org_owners             write
      by [dn children $People]   read
      by [dn children $Hosts]    read
      by [puavo_dn kadmin]       read
      by [puavo_dn kdc]          read
      by [puavo_dn monitor]      read
      by [puavo_dn puavo]        read
      by [puavo_dn puavo-ticket] read  # XXX to be removed later
      by [puavo_dn statistics]   read
      by [systemgroup getent]    read
      by [systemgroup orginfo]   read
      by *                       +dsx
}

#
# print rules
#

set rulenum 0
foreach rule $rules {
  set trimmed_rule [string trim "$rule"]
  puts "{${rulenum}}to ${trimmed_rule}"
  incr rulenum
}
