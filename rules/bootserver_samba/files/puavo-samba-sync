#!/usr/bin/perl

use strict;
use warnings;

use File::Basename;
use File::Slurper qw(read_text);
use IPC::Run qw(run);
use Net::LDAP;
use Net::LDAP::Control::SyncRequest;
use Net::LDAP::Constant qw(LDAP_SYNC_REFRESH_ONLY
                           LDAP_SYNC_REFRESH_AND_PERSIST
                           LDAP_SUCCESS);
use Sys::Syslog qw(:standard :macros);

openlog(basename($0), '', 'user');

chomp(my $bind_dn   = read_text('/etc/puavo/ldap/dn'));
chomp(my $bind_pw   = read_text('/etc/puavo/ldap/password'));
chomp(my $ldap_base = read_text('/etc/puavo/ldap/base'));
chomp(my $ldap_host = read_text('/etc/puavo/ldap/master'));

my $ldap = Net::LDAP->new($ldap_host);
unless ($ldap) {
  syslog(LOG_ERR, 'no ldap server %s', $ldap_host);
  exit(1);
}

# XXX should really use verify
my $ldap_response;
$ldap_response = $ldap->start_tls(verify => 'none');
if ($ldap_response->code != LDAP_SUCCESS) {
  syslog(LOG_ERR, 'could not start_tls on %s', $ldap_host);
  exit(1);
}

$ldap_response = $ldap->bind($bind_dn, password => $bind_pw);
if ($ldap_response->code != LDAP_SUCCESS) {
  syslog(LOG_ERR, 'failed to authenticate to %s', $ldap_host);
  exit(1);
}

my $req = Net::LDAP::Control::SyncRequest->new(
            mode => LDAP_SYNC_REFRESH_AND_PERSIST);

# XXX how to prevent multiple instances of this script running?
# XXX should there be some kind of a lock?

# XXX we could also perhaps read and update usernames and such
# XXX tracking puavoId should also be useful?  or maybe uidNumber is enough?
my @followed_attributes
  = qw(givenName sambaNTPassword puavoLocked sambaSID sn uid);

my $search = $ldap->search(base     => "ou=People,${ldap_base}",
                           scope    => 'sub',
                           control  => [ $req ],
                           callback => \&handle_search_event,
                           filter   => '(objectClass=sambaSamAccount)',
                           attrs    => \@followed_attributes);

# XXX should never get here, what to do if we do?
# XXX is $search useful ?

sub handle_search_event {
  my ($msg, $entry) = @_;

  return unless $entry;

  if ($entry->isa('Net::LDAP::Entry')) {
    handle_entry($entry);
  }
}

# XXX how to remove users that have been removed from Puavo
# XXX how to lock users that have been locked in Puavo?

sub get_samba_account_info {
  my ($uid) = @_;

  # XXX pdbedit apparently needs to use -u :-( instead if -U
  # XXX -U is only for setting and modifying, and not similar to -u

  my $status = run([ 'pdbedit', '-s', '/etc/samba/samba-ad-dc.conf',
                                '-u', $uid, '-vw' ],
                        \(my $in = ''), \my $user_info, \my $err);
  if (!$status) {
    return undef if $err =~ /Username not found/;
    die "unexpected error looking up user with ${uid}: ${err}";
  }

  my ($flags, $full_name, $nt_hash, $sid, $uid_in_samba);
  foreach (split(/\n/, $user_info)) {
    $flags        = $1     if /^Account Flags\s*:\s*\[(.*)\]$/;
    $full_name    = $1     if /^Full Name\s*:\s*(.*)$/;
    $nt_hash      = lc($1) if /^NT hash\s*:\s*(.*)$/;
    $sid          = $1     if /^User SID\s*:\s*(.*)$/;
    $uid_in_samba = $1     if /^Unix username\s*:\s*(.*)$/;
  }

  die "user account flags not found for ${uid}" unless defined($flags);
  die "user account full name not found for ${uid}" unless defined($full_name);
  die "user account NT password not found for ${uid}" unless defined($nt_hash);
  die "user account UID not found for ${uid}" unless defined($uid_in_samba);
  die "user account unix username not found for ${uid}" unless defined($uid);

  if ($uid_in_samba ne $uid) {
    die "user account info does not match user we were looking for with $uid";
  }

  {
    flags     => $flags,
    full_name => $full_name,
    nt_hash   => $nt_hash,
    sid       => $sid,
    uid       => $uid,
  }
}

sub create_user {
  my ($puavo_account_info) = @_;

  my $uid = $puavo_account_info->{uid};

  syslog(LOG_INFO, 'creating a new user %s', $uid);

  my @alnums = ('a'..'z','A'..'Z',0..9);
  my $fake_pw = join('', map { $alnums[rand @alnums] } (1..32));
  my $in = "${fake_pw}\n${fake_pw}\n";
  my $err = '';
  run([ 'pdbedit', '-s', '/etc/samba/samba-ad-dc.conf', '-a', '-u', $uid ],
      \$in, \my $out, \$err)
    or die "could not create user: $err";
}

sub update_user_information {
  my ($puavo_account_info, $samba_account_info) = @_;

  my $uid = $puavo_account_info->{uid};

  my @change_opts;

  if ($puavo_account_info->{full_name} ne $samba_account_info->{full_name}) {
    syslog(LOG_INFO, 'update full name for %s', $puavo_account_info->{uid});
    push @change_opts, '--fullname', $puavo_account_info->{full_name};
  }

  if ($puavo_account_info->{nt_hash} ne $samba_account_info->{nt_hash}) {
    syslog(LOG_INFO, 'updating user password for %s',
                     $puavo_account_info->{uid});
    push @change_opts, '--set-nt-hash', $puavo_account_info->{nt_hash};
  }

  return unless @change_opts;

  my $in  = '';
  my $err = '';
  run([ 'pdbedit', '-r', '-s', '/etc/samba/samba-ad-dc.conf', '-u', $uid,
                   @change_opts ],
      \$in, \my $out, \$err)
    or die "could not update user ${uid} information: ${err}";
}

sub handle_entry {
  my ($entry) = @_;

  # XXX we are also interested in the puavoLocked attribute
  # XXX what kind of event we get when an account is REMOVED from Puavo?

  foreach (qw(givenName sambaNTPassword sambaSID sn uid)) {
    unless ($entry->exists($_)) {
      syslog(LOG_WARNING, "got an entry without '%s' attribute", $_);
      return;
    }
  }

  my $uid = $entry->get_value('uid');

  my $puavo_account_info = {
    full_name => $entry->get_value('givenName')
                    . ' ' . $entry->get_value('sn'),
    locked    => $entry->get_value('puavoLocked'),
    nt_hash   => $entry->get_value('sambaNTPassword'),
    sid       => $entry->get_value('sambaSID'),
    uid       => $uid,
  };

  eval {
    my $samba_account_info = get_samba_account_info($uid);

    unless (defined($samba_account_info)) {
      create_user($puavo_account_info);
      $samba_account_info = get_samba_account_info($uid);
      die("could not get user account info for ${uid}"
            . ' (even though it was just created)')
        unless defined($samba_account_info);
    }

    update_user_information($puavo_account_info, $samba_account_info);
  };
  if ($@) {
    chomp $@;
    syslog(LOG_ERR, 'error in updating user information: %s', $@);
  }
}
